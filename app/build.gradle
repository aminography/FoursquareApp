apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

String apkName = "FoursquareApp"
int appVersionCode = 1
String appVersionName = "1.0"

project.archivesBaseName = apkName + "-" + appVersionName

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.aminography.foursquareapp"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file(keystoreProperties['debugKeyStoreFile'])
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storePassword keystoreProperties['debugStorePassword']
        }
        release {
            storeFile file(keystoreProperties['releaseKeyStoreFile'])
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
            storePassword keystoreProperties['releaseStorePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            debuggable true
            applicationIdSuffix '.debug'
        }

        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            debuggable false
            shrinkResources true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.1.0'

    // Coroutine
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // Anko
    def anko_version = "0.10.8"
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"

    // View
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Image Loader
    implementation 'com.github.bumptech.glide:glide:4.9.0'

    // AndroidX
    def androidx_version = "1.2.0-rc01"
    implementation "androidx.core:core-ktx:$androidx_version"

    // Lifecycle
    def lifecycle_version = "2.2.0-rc03"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    // Database
    def room_version = "2.2.3"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

//    // Pagination
//    implementation "androidx.paging:paging-runtime:2.1.0"

    // Dependency Injection
    def koin_version = "1.0.2"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"

    // Webservice
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.1'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.14.1'
    implementation 'com.squareup.okhttp3:okhttp:3.14.1'

    // Map & Location
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation "com.google.android.gms:play-services-location:17.0.0"

    // Firebase & Crashlytics
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // MultiDex
    implementation 'androidx.multidex:multidex:2.0.1'

//    // Leak Detection
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-alpha-3'

    // Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply plugin: 'com.google.gms.google-services'

task moveDebugVersionFile {
    doLast {
        mkdir("../versions/${appVersionName}")
        file("../app/build/outputs/apk/debug/${project.archivesBaseName}-debug.apk").renameTo(file("../versions/${appVersionName}/${project.archivesBaseName}-debug.apk"))
    }
}

task moveReleaseVersionFile {
    doLast {
        mkdir("../versions/${appVersionName}")
        file("../app/build/outputs/apk/release/${project.archivesBaseName}-release.apk").renameTo(file("../versions/${appVersionName}/${project.archivesBaseName}-release.apk"))
    }
}
